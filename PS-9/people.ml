(*
                          CS 51 Problem Set
                 Simulation of an Infectious Disease

                      People in the simulation
 *)

module G = Graphics ;;
open Config ;;
open Registry ;;
open Utilities ;;
module Ctr = Counter ;;
module Viz = Visualization ;;
module Stat = Statistics ;;
(* also uses Utilities *)

(*....................................................................
                                People
 *)

class person (initx : int) (inity : int)
             (initstepsize : int)
             (initinfect : float) =
  object (self)
    val id : string = Utilities.gensym ()
    val mutable posx : int = initx
    val mutable posy : int = inity
    val mutable step_size : int = initstepsize
    val mutable infectiousness : float = initinfect

    method id : string = id
    method step_size : int = step_size
    method infectiousness : float = infectiousness

    method set_pos (x : int) (y : int) : unit =
      posx <- x;
      posy <- y
    method pos = posx, posy

    method set_step_size (new_step_size : int) : unit =
      step_size <- new_step_size

    method set_infectiousness (new_infect : float) : unit =
      infectiousness <- new_infect

    method move : unit =
      let x, y = self#pos in
      let newx, newy =
        Utilities.rand_step x y self#step_size in
      (* drop from old location in registry *)
      Registry.deregister (self :> thing_type);
      (* update location *)
      self#set_pos newx newy;
      (* re-add at the new location *)
      Registry.register (self :> thing_type)

    method update : unit =
      self#move

    method draw : unit =
      let x, y = self#pos in
      Viz.draw_circle x y G.black
  end ;;

(*....................................................................
                       People in various states

  Note that since these classes refer to each other, they must be
  simultaneously defined using `and` instead of sequentially defined
  as separate classes.
 *)

class susceptible (initx : int) (inity : int) =
  object (self)
    inherit person initx inity
                   cSTEP_SIZE_SUSCEPTIBLE
                   cINFECTIOUSNESS_SUSCEPTIBLE
            as super

    initializer
      Stat.susceptible#bump

    method! update =
      super#update;
      let posx, posy = self#pos in
      let infectiousness_total =
        (* calculate total infectiousness of all neighbors *)
        Utilities.sum_float
	  (List.map (fun obj -> obj#infectiousness)
                    (Registry.neighbors (self :> thing_type))) in
      if Utilities.flip_coin infectiousness_total then
        (* infected, so update the registry by replacing this object
           with an infected one *)
        begin
          Stat.susceptible#debump;
          Registry.deregister (self :> thing_type);
          Registry.register ((new infected posx posy) :> thing_type)
        end

    method! draw =
      let x, y = self#pos in
      Viz.draw_circle x y cCOLOR_SUSCEPTIBLE
  end





and (* class *) infected (initx : int) (inity : int) =
  object (thing)
    inherit person initx inity
                   cSTEP_SIZE_INFECTED
                   cINFECTIOUSNESS_INFECTED as super

    initializer
      Stat.infected#bump

    val mutable req_time_recovery =
      let mean, stdev = cRECOVERY_PERIOD in
      gaussian mean stdev

    method! update =
      super#update;
      let posx, posy = thing#pos in
      if req_time_recovery > 0. then
      req_time_recovery <- req_time_recovery -. 1.
      else if flip_coin cMORTALITY then
        begin
            Stat.infected#debump;
            Registry.deregister (thing :> thing_type);
            Registry.register ((new deceased posx posy) :> thing_type)
        end
      else
        begin
            Stat.infected#debump;
            Registry.deregister (thing :> thing_type);
            Registry.register ((new recovered posx posy) :> thing_type)
        end

    method! draw =
      let x, y = thing#pos in
      Viz.draw_circle x y cCOLOR_INFECTED;
      Viz.draw_circle x y cCOLOR_INFECTED ~size: (cSYMBOL_SIZE + cNEIGHBOR_RADIUS) ~filled: false
  end




and (* class *) recovered (initx : int) (inity : int) =
  object(self)
    inherit person initx inity
                 cSTEP_SIZE_RECOVERED
                 cINFECTIOUSNESS_RECOVERED as super

    initializer
      Stat.recovered#bump

    val mutable req_time_immune =
      let mean, stdev = cIMMUNITY_PERIOD in
      gaussian mean stdev


    method! update =
      super#update;
      let posx, posy = self#pos in
      if req_time_immune > 0. then
      req_time_immune <- req_time_immune -. 1.
      else
        begin
          Stat.recovered#debump;
          Registry.deregister (self :> thing_type);
          Registry.register ((new susceptible posx posy) :> thing_type)
        end


    method! draw =
      let x, y = self#pos in
      Viz.draw_circle x y cCOLOR_RECOVERED;

  end

and (* class *) deceased (initx : int) (inity : int) =
  object(self)
    inherit person initx inity
                 cSTEP_SIZE_DECEASED
                 cINFECTIOUSNESS_DECEASED

  initializer
    Stat.deceased#bump

  method! update =
    self#set_pos initx inity

  method! draw =
    let x, y = self#pos in
      Viz.draw_cross x y cCOLOR_DECEASED
  end
